DEFINE_PATCH_FUNCTION ~HANDLE_TARGET_GROUP~
INT_VAR
"targ" = 0
"overrideDur" = 0
"dispel" = 3
STR_VAR
"overrideName" = ""
BEGIN
  insertPos = "-1" // Start by assuming the messages should go as the last effects
  READ_STRREF 0x08 spellName
  PATCH_IF NOT ("%overrideName%" STRING_EQUAL "") THEN BEGIN
    TEXT_SPRINT spellName "%overrideName%"
  END
  READ_LONG   0x64 abOffset
  READ_SHORT  0x68 abTotal
  READ_LONG   0x6a efOffset
  FOR (abNum = 0; abNum < abTotal; abNum += 1) BEGIN
    //find the effect with the longest duration
    READ_SHORT (abOffset + (0x28 * abNum) + 0x1E) thisEfNum
    READ_SHORT (abOffset + (0x28 * abNum) + 0x20) thisEfFirst
    longestDur = 0
    FOR (efNum = 0; efNum < thisEfNum; efNum += 1) BEGIN
      READ_LONG (efOffset + (0x30 * (efNum + thisEfFirst)) + 0xE) dur
	  PATCH_IF dur > longestDur BEGIN
	    longestDur = dur
	  END
	  READ_SHORT (efOffset + (0x30 * (efNum + thisEfFirst)) + 0x0) opcode
	  PATCH_IF opcode = 206 BEGIN //Found a protection from spell opcode BEGIN
		READ_ASCII (efOffset + (0x30 * (efNum + thisEfFirst)) + 0x14) protName // Find out which spell
		PATCH_IF ~%protName%.spl~ STR_EQ ~%SOURCE_FILE%~ BEGIN //If it's the one we're currently changing
		  insertPos = efNum //Insert messages before the protection from spell effect
		END
	  END
	END
    PATCH_IF overrideDur > 0 THEN BEGIN
      longestDur = overrideDur
    END
	headerNum = abNum + 1
	stringToAdd = RESOLVE_STR_REF(@200)
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 header = headerNum target = targ timing = 4 resist_dispel = dispel duration = longestDur parameter1 = stringToAdd insert_point = insertPos END
    PATCH_FOR_EACH limit IN 6 12 30 60 120 BEGIN
      PATCH_IF longestDur > limit BEGIN
	    dur = longestDur - limit
		stringToAdd = RESOLVE_STR_REF(@201)
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 header = headerNum target = targ timing = 4 resist_dispel = dispel duration = dur parameter1 = stringToAdd insert_point = insertPos END
      END
    END
  END
END

//Function to get the name of the given spell as a string.
DEFINE_ACTION_FUNCTION ~GET_SPELL_NAME~ STR_VAR spell_name="" RET name BEGIN
	COPY_EXISTING ~%spell_name%.SPL~ ~override~
		READ_STRREF 0x08 name
    BUT_ONLY
END

//Fix glitch that causes expiration notices to be wrong at high levels for Holy Power
ACTION_IF FILE_EXISTS_IN_GAME ~SPPR412.SPL~ BEGIN
	COPY_EXISTING ~SPPR412.SPL~ ~override~
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode = 141 duration = 0 END
	BUT_ONLY
END

//Buffs that only target the caster and can be dispelled
ACTION_FOR_EACH ~spell~ IN 
			   ~SPPR109~ ~SPPR110~ ~SPPR111~
			   ~SPPR206~ ~SPPR213~ ~SPPR214~
			   ~SPPR412~ ~SPPR416~
			   ~SPPR505~ ~SPPR513~ ~SPPR515~
			   ~SPPR603~ ~SPPR613~ ~SPPR698~
			   ~SPPR701~ ~SPPR725~ ~SPPR730~
			   ~SPWI102~ ~SPWI107~ ~SPWI114~ ~SPWI120~
			   ~SPWI201~ ~SPWI212~ ~SPWI219~ ~SPWI222~
			   ~SPWI317~ ~SPWI318~ ~SPWI315~
			   ~SPWI403~ ~SPWI406~ ~SPWI416~ ~SPWI418~
			   ~SPWI505~ ~SPWI511~ ~SPWI518~ ~SPWI519~ ~SPWI522~ ~SPWI590~ ~SPWI591~ ~SPWI592~
			   ~SPWI593~ ~SPWI594~ ~SPWI595~ ~SPWI596~ ~SPWI597~
			   ~SPWI602~ ~SPWI603~ ~SPWI607~ ~SPWI609~ ~SPWI611~ ~SPWI618~
			   ~SPWI701~ ~SPWI703~ ~SPWI708~ ~SPWI723~
			   ~SPWI804~ ~SPWI808~
			   ~SPWI902~ ~SPWI907~ ~SPWI915~ ~SPWI916~
			   ~SPIN103~
			   ~SPCL232~ ~SPCL237~  
			   ~SPCL721~ ~SPCL731~
			   ~SPCL732~ ~SPCL741~
			   ~SPDM102~ ~SPDM105~ ~SPDM106~
			   ~BHAAL1B~ ~BHAAL2A~ ~BHAAL3B~
  BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%spell%.SPL~ THEN BEGIN
    COPY_EXISTING ~%spell%.SPL~ ~override~
      LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 1 END
    BUT_ONLY
  END
END

//Buffs that only target the caster and can't be dispelled
ACTION_FOR_EACH spell IN ~SPCL152~ ~SPCL238~ ~SPCL321~ ~SPCL423~ ~SPCL521~ ~SPCL522~
						 ~SPCL904~ ~SPCL907~ ~SPCL913~ ~SPCL914~ ~SPCL917~ ~SPCL941~
						 ~SPDWD02~
BEGIN
	COPY_EXISTING ~%spell%.spl~ ~override~
		LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 1 dispel = 2 END
	IF_EXISTS
END

//Buffs that only target the caster are natural abilities?
ACTION_FOR_EACH spell IN ~SPCL938~ ~SPCL940~
BEGIN
	COPY_EXISTING ~%spell%.spl~ ~override~
		LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 1 dispel = 0 END
	IF_EXISTS
END

//Buffs that affect a target
ACTION_FOR_EACH ~spell~ IN 
			   ~SPPR101~ ~SPPR107~ ~SPPR108~ ~SPPR150~
			   ~SPPR201~ ~SPPR202~ ~SPPR210~
			   ~SPPR306~ ~SPPR312~
			   ~SPPR403~ ~SPPR407~ ~SPPR408~ ~SPPR409~ ~SPPR413~
			   ~SPPR507~ ~SPPR508~ ~SPPR509~
			   ~SPPR711~ 
			   ~SPWI108~ ~SPWI113~
			   ~SPWI209~ ~SPWI210~ ~SPWI214~
			   ~SPWI305~ ~SPWI311~ ~SPWI319~ ~SPWI320~
			   ~SPWI405~ ~SPWI414~
			   ~SPWI512~ ~SPWI517~
			   ~SPWI613~ ~SPWI606~
			   ~SPWI702~ ~SPWI721~
			   ~SPWI803~
			   ~SPDR401~
			   ~SPCL213~
  BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%spell%.SPL~ THEN BEGIN
    COPY_EXISTING ~%spell%.SPL~ ~override~
      LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 2 END
    BUT_ONLY
  END
END

//spells that the longest duration isn't accurate for
ACTION_IF FILE_EXISTS_IN_GAME ~SPWI921.SPL~ THEN BEGIN
  COPY_EXISTING ~SPWI921.SPL~ ~override~
    LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 1 overrideDur = 12 END
  BUT_ONLY
END

ACTION_IF FILE_EXISTS_IN_GAME ~DW#QALAC.SPL~ THEN BEGIN
  COPY_EXISTING ~DW#QALAC.SPL~ ~override~
    LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 1 overrideDur = 12 END
  BUT_ONLY
END

LAF ~GET_SPELL_NAME~ STR_VAR spell_name = ~SPPR203~ RET ovName = name END

//fixpack version of chant
ACTION_IF FILE_EXISTS_IN_GAME ~SPPR203D.spl~ BEGIN
  COPY_EXISTING ~SPPR203D.spl~ ~override~
    LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 2 STR_VAR overrideName = EVALUATE_BUFFER ~%ovName%~ END 
  BUT_ONLY
END

//SCS added prebuff spells
ACTION_FOR_EACH filename IN 
	~dwsc721~
	~dwsp110~ ~dwsp111~
	~dwsp202~ ~dwsp206~ ~dwsp213~ ~dwsp214~ ~dwsp218~
	~dwsp306~ ~dwsp324~ ~dwsp328~
	~dwsp403~ ~dwsp409~ ~dwsp412~ 
	~dwsp506~ ~dwsp508~ ~dwsp509~ ~dwsp513~ ~dwsp525~
	~dwsp603~ ~dwsp613~ ~dwsp615~ ~dwsp698~ 
	~dwsp701~ ~dwsp711~ ~dwsp721~ ~dwsp725~ ~dwsp730~ ~dwsp735~
	~dwsw102~ ~dwsw114~ 
	~dwsw206~ ~dwsw212~ 
    ~dwsw310~ ~dwsw311~ ~dwsw317~ ~dwsw318~ ~dwsw319~ ~dwsw320~ 
	~dwsw403~ ~dwsw405~ ~dwsw406~ ~dwsw414~ ~dwsw418~ 
	~dwsw505~ ~dwsw512~ ~dwsw517~ ~dwsw518~ ~dwsw519~ ~dwsw522~ ~dwsw590~ ~dwsw592~ 
	~dwsw602~ ~dwsw606~ ~dwsw613~ ~dwsw618~
	~dwsw701~ ~dwsw702~ ~dwsw723~ ~dwsw726~
	~dwsw806~
	~dwsw902~ ~dwsw920~ 
	~MELSPL02~ ~MELSPL03~
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%filename%.spl~ BEGIN
    COPY_EXISTING ~%filename%.spl~ ~override~
      LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 1 END
    BUT_ONLY
  END
END

//SCS added prebuff spells
ACTION_FOR_EACH filename IN 
	~dwsp101~ ~dwsp108~
	~dwsp406~
	~dwsw305~
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%filename%.spl~ BEGIN
    COPY_EXISTING ~%filename%.spl~ ~override~
      LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 2 END
    BUT_ONLY
  END
END

//Rogue rebalancing added spells
ACTION_FOR_EACH filename IN ~RR#THL02~ ~RR#THL03~ ~RR#WDIS~ ~RR#BHL05~ ~RR#BHL06~
BEGIN
	COPY_EXISTING ~%filename%.spl~ ~override~
		LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 1 dispel = 0 END
	IF_EXISTS
END

//Time stop
ACTION_IF FILE_EXISTS_IN_GAME ~SPWI909.SPL~ BEGIN
	COPY_EXISTING ~SPWI909.SPL~ ~override~
		PATCH_FOR_EACH limit IN 1 2 3 4 5 6 12 BEGIN
			dur = 18 - limit
			limit = limit - 1
			stringToAdd = RESOLVE_STR_REF(@202)
			LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 1 timing = 4 header = 1 parameter1 = stringToAdd resist_dispel = 3 duration = dur END
		END
	BUT_ONLY
END

//Time trap
ACTION_IF FILE_EXISTS_IN_GAME ~SPCL912B.SPL~ BEGIN
	COPY_EXISTING ~SPCL912B.SPL~ ~override~
		PATCH_FOR_EACH limit IN 1 2 3 4 5 6 BEGIN
			dur = 10 - limit
			limit = limit - 1
			stringToAdd = RESOLVE_STR_REF(@203)
			LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 9 timing = 4 header = 1 parameter1 = stringToAdd resist_dispel = 3 duration = dur END
		END
	BUT_ONLY
END

//Wish time stop and improved alacrity
ACTION_FOR_EACH filename IN ~SPWISH17.SPL~ ~DW#WISH3.SPL~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%filename%~ BEGIN
		COPY_EXISTING ~%filename%~ ~override~
			PATCH_FOR_EACH limit IN 12 6 BEGIN
				dur = 24 - limit
				stringToAdd = RESOLVE_STR_REF(@204)
				LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 1 timing = 4 header = 1 parameter1 = stringToAdd resist_dispel = 3 duration = dur END
			END
			stringToAdd = RESOLVE_STR_REF(@205)
			LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 1 timing = 4 header = 1 parameter1 = stringToAdd resist_dispel = 3 duration = 24 END
			PATCH_FOR_EACH limit IN 1 2 3 4 5 6 12 BEGIN
				dur = 36 - limit
				limit = limit - 1
				stringToAdd = RESOLVE_STR_REF(@206)
				LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 1 timing = 4 header = 1 parameter1 = stringToAdd resist_dispel = 3 duration = dur END
			END
		BUT_ONLY
	END
END

//IWD Divine Spells - player versions. I'm separating these out just in case
//there is another mod that adds spells that use the same files
ACTION_IF FILE_EXISTS_IN_GAME ~#PRAYERG.SPL~ BEGIN

	//Buffs that only target the caster and can be dispelled
	ACTION_FOR_EACH ~spell~ IN 
		~SPPR218~
		~SPPR323~ ~SPPR328~
		~SPPR424~
		~SPPR520~
		~SPPR615~
		~SPPR737~ ~SPPR735~
	BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%spell%.SPL~ THEN BEGIN
		COPY_EXISTING ~%spell%.SPL~ ~override~
			LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 1 END
		BUT_ONLY
	  END
	END

	//Buffs that only target the caster and can't be dispelled
	ACTION_FOR_EACH spell IN
		~SPPR324~
	BEGIN
		COPY_EXISTING ~%spell%.spl~ ~override~
			LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 1 dispel = 2 END
		IF_EXISTS
	END

	//Buffs that affect a target
	ACTION_FOR_EACH ~spell~ IN 
		~SPPR322~
		~SPPR421~
		~SPPR522~ 
	BEGIN
	  ACTION_IF FILE_EXISTS_IN_GAME ~%spell%.SPL~ THEN BEGIN
		COPY_EXISTING ~%spell%.SPL~ ~override~
		  LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 2 END
		BUT_ONLY
	  END
	END
	
	//Buffs that affect a target and need a name override
	LAF ~GET_SPELL_NAME~ STR_VAR spell_name = ~SPPR320~ RET ovName = name END
	
	ACTION_IF FILE_EXISTS_IN_GAME ~#PRAYERG.SPL~ THEN BEGIN
		COPY_EXISTING ~#PRAYERG.SPL~ ~override~
			LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 2 STR_VAR overrideName = EVALUATE_BUFFER ~%ovName%~ END
		BUT_ONLY
	END
	
	LAF ~GET_SPELL_NAME~ STR_VAR spell_name = ~SPPR420~ RET ovName = name END
	ACTION_IF FILE_EXISTS_IN_GAME ~#RECITEG.SPL~ THEN BEGIN
		COPY_EXISTING ~#RECITEG.SPL~ ~override~
			LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 2 STR_VAR overrideName = EVALUATE_BUFFER ~%ovName%~ END
		BUT_ONLY
	END
	
	
	LAF ~GET_SPELL_NAME~ STR_VAR spell_name = ~SPPR518~ RET ovName = name END
	ACTION_FOR_EACH ~spell~ IN 
		~SPPR518A~ ~SPPR518B~
	BEGIN
	  ACTION_IF FILE_EXISTS_IN_GAME ~%spell%.SPL~ THEN BEGIN
		COPY_EXISTING ~%spell%.SPL~ ~override~
		  LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 2 STR_VAR overrideName = EVALUATE_BUFFER ~%ovName%~ END
		BUT_ONLY
	  END
	END
END

//IWD Arcane Spells - player versions. I'm separating these out just in case
//there is another mod that adds spells that use the same files
ACTION_IF FILE_EXISTS_IN_GAME ~DOBONE.ITM~ BEGIN

	//Buffs that only target the caster and can be dispelled
	ACTION_FOR_EACH ~spell~ IN 
		~SPWI126~
		~SPWI226~
		~SPWI627~ ~SPWI631~ ~SPWI629~
		~SPWI728~
		~SPWI820~ ~SPWI806~
	BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%spell%.SPL~ THEN BEGIN
		COPY_EXISTING ~%spell%.SPL~ ~override~
			LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 1 END
		BUT_ONLY
	  END
	END

	//Buffs that only target the caster and can't be dispelled
	ACTION_FOR_EACH spell IN
		~SPWI630~
	BEGIN
		COPY_EXISTING ~%spell%.spl~ ~override~
			LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 1 dispel = 2 END
		IF_EXISTS
	END

	//Buffs that affect a target
	ACTION_FOR_EACH ~spell~ IN 
		~SPWI227~
		~SPWI426~ ~SPWI427~
	BEGIN
	  ACTION_IF FILE_EXISTS_IN_GAME ~%spell%.SPL~ THEN BEGIN
		COPY_EXISTING ~%spell%.SPL~ ~override~
		  LPF ~HANDLE_TARGET_GROUP~ INT_VAR targ = 2 END
		BUT_ONLY
	  END
	END
END
